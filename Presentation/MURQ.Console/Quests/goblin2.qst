:0
cls
invkill
perkill
pln Квест про гобліна
pln
btn 4, Почати гру
btn 1, Передісторія
btn 2, Автори
btn 3, Допомога
end

:die
pln
pln **КІНЕЦЬ ГРИ**
pln На жаль, ви не змогли допомогти Ґрібззу вижити.
pln 
btn 0, На початок
end

:1
pln **Передісторія**
pln Колись Ґрібзз був звичайним гобліном — хитрим, вертким і до нестями допитливим. Але ця допитливість завела його занадто далеко: він потрапив до рук некромантів, які забрали його на свій корабель як живу жертву для ритуалів.
pln У темному трюмі, закутий у клітку, він провів тижні — спостерігав, слухав, запам'ятовував. І мовчав.
btn 0, Назад
end

:2 
pln **Автори**
pln Короткий текстовий квест за мотивами гри Темний Мессія.               
pln Автор: Apromix               
btn 0, Назад
end

:3
pln **Допомога**
pln Ви граєте гобліном Ґрібззом. Виграти можна кількома способами.
pln Будьте обережні — не всі рішення є безпечними!
btn 0, Назад
end

:4
pln Одного дня хтось відчинив клітку Ґрібзза. Це був Сарет, учень мага, який захопив судно, щоб дістатись до загадкового острова.
pln Сарет навіть не звернув уваги на гобліна — просто відкрив засув. А Ґрібзз миттю зник у тіні з криком: "Вільний! Вільний як вітер! Хе-хе-хе!"
pln Що робити далі?
btn 5, Покинути корабель через відкритий люк
btn 6, Залишитися на кораблі й сховатися
btn 7, Атакувати Сарета 
end

:5
pln Ти вискакуєш через люк і в останню мить бачиш відкрите море. Хоча корабель і не далеко від берега, але ти не вмієш плавати.
pln Ти каменем падаєш у воду і тонеш. Мабуть, варто було обрати інший шлях.
goto die
end

:7
pln Ти кидаєшся на Сарета, але він — досвідчений маг. Одним жестом він запускає в тебе магічну блискавку.
pln "Дурний гоблін," — останні слова, які ти чуєш.
goto die
end

:6
pln Ти ховаєшся серед мотлоху в трюмі у невеликій технічній кімнатці, заваленій різним мотлохом. Ніхто тебе не шукає — маги зайняті своїми справами. Скоріш за все ніхто і не підозрює, що ти на кораблі.
btn 999, Заснути спокійним сном

:999
pln Минає кілька днів. Ти чуєш, як команда говорить про наближення до якогось острова. Нарешті ти стаєш сміливішим і вирішуєш все навколо оглянути.
btn 44, Оглянути коридор

:44
pln Ти обережно вибираєшся в коридор і прислухаєшся. Нікого.
btn 188, Обшукати сусідню кімнату зліва
btn 489, Обшукати сусідню кімнату справа
btn 9, Повернутись і лягти спати
end

:188
pln Ти обережно прокрадаєшся в тісну кімнатку схожу на ту, в якій ти зараз тимчасово оселився.
btn 955, Обшукати тут все
btn 44, Повернутись до коридору
end

:955
pln Під час обшуку цієї кімнати ти знаходиш тіло мертвого воїна, якого, схоже, забули викинути в море після захоплення корабля.
btn 8, Обшукати тіло воїна
btn 44, Повернутись в коридор
end

:489
pln Ти обшукуєш кімнату і знаходиш мертвого некроманта, якого не позбулись маги.
pln На шиї у нього виблискує дивний амулет. Ти береш його в руки. Амулет здається теплим на дотик.
pln "Цікаво," — думаєш ти, ховаючи знахідку в кишені.
amulet = 1
Inv+ Таємничий амулет
btn 44, Повернутись в коридор
end

:8
pln Ти обшукуєш тіло і знаходиш короткий іржавий ніж, лезо якого вкрито дивним узором.
pln "Пригодиться," — думаєш ти, ховаючи знахідку в своїх лахміттях.
knife = 1
Inv+ Іржавий ніж
btn 44, Повернутись в коридор
end

:9
pln Наступного дня корабель потрапляє в жахливий шторм. Трюмну кімнату заливає водою, і ти розумієш, що треба швидко щось робити. Тієї ж миті величезна хвиля б'є в корабель і люк над тобою ламається.
btn 444, Вибратися на палубу
btn 12, Залишитися в кімнаті
end

:444
pln Ти швидко вискакуєш на палубу і тебе миттю з ніг збиває хвиля. Вода швидко тебе тягне за борт. Ти пробуєш схопитись хочаб за щось щоб не змило в море.
btn 747, Схопитись за канати
btn 630, Схопитись за обломки
end

:747
pln Канат витримує і ти піднімаєшся на ноги. Навколо реве розбурхане море і дме сильний вітер. Злякані маги пробують керувати кораблем. Височезні мачти гнуться і скриплять під поривами вітру. Неподалік розгойдується рятувальний човен.
btn 109, Швидко бігти до мачти
if knife = 1 then btn 11, Рятуватись на човні
end

:630
pln Ти хапаєшся за якісь обломки і тебе змиває за борт. Нова хвиля накриває тебе з головою і ти назавжди зникаєш під водою.
goto die
end

:11
pln В такий шторм ніхто не звертає уваги на маленького гобліна. Ти добираєшся до рятівного човна. Тепер треба його спустити на воду.
btn 10, Знову оглянути палубу
btn 888, Швидко перерізати канати, що утримують човен
end

:888
pln Ти перерізаєш канати і звільняєш човен, але нова величезна хвиля змиває і тебе, і човен за борт. Ти падаєш в розбурхане море і на тебе падає човен.
goto die
end

:12
pln Вода прибуває дуже швидко. Ти не встигаєш вибратися, і кімната повністю заповнюється водою. На жаль, гобліни не вміють дихати під водою.
goto die
end

:109
pln Ти і трьох кроків не встигаєш зробити в сторону мачти. Величезна хвиля накриває корабель і змиває тебе за борт.
goto 13
end

:10
pln Навколо хаос. Маги намагаються протистояти шторму. Виходить це у них так собі.
pln Раптово величезна хвиля накриває корабель і змиває тебе за борт.
goto 13
end

:13
pln Холодна вода обпікає тобі шкіру. Ти відчайдушно борешся з хвилями, але сили нерівні.
pln Саме коли ти думаєш, що все скінчено, тебе підхоплює особливо велика хвиля і несе кудись.
pln Ти втрачаєш свідомість...
btn 14, Прийти до тями
end

:14
pln Ти повільно приходиш до тями, відчуваючи, як піщинки впиваються у твою зелену шкіру.
pln Горло пече від солоної води, а тіло болить після твоєї "подорожі" морем.
pln Ти на пляжі невідомого острова. Тобі не відомо скільки минуло часу.
beachvisited = 1
goto beach
end

:beach
pln Ти на піщаному пляжі. Попереду видніється лінія джунглів, ліворуч пляж тягнеться далі, праворуч — скелясті пагорби.
if beachchecked = 0 then btn beachchecked, Оглянути пляж
btn 15, Йти у джунглі
btn 16, Йти вздовж пляжу ліворуч
btn 17, Підійти до скелястих пагорбів
if knife = 0 and seeshell = 1 then btn getshell, Підняти гарну мушлю
if stick = 1 and seeshell = 1 then btn getstickshell, Підняти мушлю палицею
if amulet = 1 and amuletused = 0 then btn useamulet, Надіти амулет
if stickfish = 1 and bamboo = 1 and rope = 1 then btn makeraft, Збудувати пліт
end

:beachchecked
beachchecked = 1
if seeshell = 0 then pln Оглядаючи пляж, ти знаходиш красиву велику мушлю і трохи мотузки від корабельних снастей.
if seeshell = 0 then seeshell = 1
if rope = 0 then rope = 1
if rope = 1 then Inv+ Мотузка
pln На піску ти помічаєш дивні сліди — схожі на людські. Вони ведуть у джунглі.
btn beach, Назад
end

:getshell
pln Ти піднімаєш мушлю і раптом бачиш, як з-під неї виповзає отруйна змія! Вона кусає тебе, перш ніж ти встигаєш відскочити.
pln На жаль, її отрута дуже сильна для маленького гобліна.
goto die
end

:getstickshell
pln Ти обережно підважуєш мушлю палицею і бачиш, як з-під неї виповзає отруйна змія! 
pln Гарна ідея з палицею — тепер ти знаєш про небезпеку і можеш безпечно обійти її.
pln Почекавши, доки змія відповзе, ти підбираєш мушлю.
seeshell = 0
shell = 1
Inv+ Красива мушля
btn beach, Назад
end

:useamulet
pln Ти надягаєш амулет на шию. Коли холодний метал торкається твоєї шкіри, ти відчуваєш дивне тепло.
pln Раптово, світ навколо тебе змінюється. Ти все ще бачиш пляж і море, але тепер також помічаєш тонкі нитки енергії, що пронизують землю.
pln Найсильніший слід тягнеться до скелястих пагорбів.
amuletused = 1
btn beach, Назад
end

:15
pln Ти обережно входиш у джунглі. Тут темно і волого, повітря насичене запахами рослин і прілого листя. Вперед веде невелика стежина.
junglevisited = 1
goto jungle
end

:jungle
pln Ти серед густих джунглів. Навколо багато дивних рослин.
if fruit = 0 then pln На найближчому кущі ти бачиш яскраво-червоні плоди, які виглядають соковитими.
if fruit = 0 then btn getfruit, Скуштувати червоний плід
if bamboo = 0 and jungleexplored = 1 then btn getbamboo, Зрізати бамбук
if stickfish = 0 and jungleexplored = 1 then btn fishing, Спробувати половити рибу
btn 30, Повернутися на пляж
if jungleexplored = 0 then btn jungleexplored, Дослідити джунглі далі
if hutheard = 1 then btn 40, Піти до хатини
end

:getfruit
pln Ти скуштував яскраво-червоний плід. Він неймовірно солодкий і смачний!
pln Через деякий час ти помічаєш, що твій зір став гострішим, а відчуття — сильнішими.
fruit = 1
fruitpower = 1
Inv+ Червоний плід
btn jungle, Назад
end

:jungleexplored
jungleexplored = 1
pln Досліджуючи джунглі, ти знаходиш: 
pln - Зарості бамбуку, який виглядає достатньо міцним
pln - Невелику лагуну з прісною водою, де плавають риби
pln
pln Ще далі в джунглах ти помічаєш дим, що піднімається між дерев.
hutheard = 1
btn jungle, Назад
end

:getbamboo
if knife = 1 then pln За допомогою іржавого ножа ти зрізаєш кілька міцних бамбукових жердин.
if knife = 1 then bamboo = 1
if knife = 1 then Inv+ Бамбукові жердини
if knife = 0 then pln Тобі потрібно щось гостре, щоб зрізати бамбук.
btn jungle, Назад
end

:fishing
if stick = 0 then pln Тобі потрібна якась палиця, щоб зробити спис для риболовлі.
if stick = 0 then btn jungle, Назад
if stick = 1 then pln За допомогою загостреної палиці ти ловиш кілька невеликих рибок в лагуні.
if stick = 1 then stickfish = 1
if stick = 1 then Inv+ Свіжа риба
if stick = 1 then btn jungle, Назад
end

:16
pln Ти йдеш уздовж пляжу. Піщана смуга стає вужчою, а джунглі підходять ближче до води.
pln На піску ти помічаєш сліди, схожі на людські. Вони ведуть у джунглі.
pln Попереду ти бачиш прибиту до берега гнилу колоду.
btn beach, Повернутися
btn 20, Оглянути колоду
btn 21, Слідувати за слідами в джунглі
end

:20
pln Ти підходиш до колоди і бачиш, що це не просто дерево, а частина рятувального човна з корабля.
pln Серед трухлявої деревини ти знаходиш уламок весла, який можна використати як палицю.
stick = 1
Inv+ Дерев'яна палиця
btn 16, Назад
end

:21
pln Ти обережно слідуєш за слідами в джунглі. Через деякий час виходиш на маленьку галявину, де стоїть примітивна хатина.
pln З димаря іде дим — хтось явно всередині.
goto 40
end

:17
if amuletused = 0 then pln Ти підходиш до скелястих пагорбів. Вони виглядають крутими і небезпечними для сходження.
if amuletused = 0 then btn beach, Повернутись на пляж
if amuletused = 0 then btn climbrock, Спробувати видертися на скелі
if amuletused = 1 then pln Ти підходиш до скелястих пагорбів. Завдяки амулету, ти бачиш слабке світіння, що виходить з невеликої печери біля підніжжя скелі.
if amuletused = 1 then btn beach, Повернутись на пляж
if amuletused = 1 then btn entercave, Увійти в печеру
end

:climbrock
pln Ти намагаєшся видертися на скелясті пагорби, але камені дуже слизькі від бризок океану.
pln Ти зриваєшся і падаєш, сильно вдарившись головою об гострий камінь. І помираєш.
goto die
end

:entercave
pln Ти входиш у маленьку печеру. Всередині темно, але амулет слабко світиться, даючи трохи світла.
pln В глибині печери ти бачиш дивний кам'яний вівтар. На ньому лежить маленький предмет, що сильно світиться в світлі амулета.
btn beach, Втекти з печери
btn takestone, Взяти предмет з вівтаря
end

:takestone
pln Ти береш предмет з вівтаря — це невеликий камінь з дивними символами.
pln Як тільки ти його торкаєшся, амулет на твоїх грудях спалахує яскравим світлом.
pln Ти відчуваєш, що камінь і амулет якось пов'язані.
magicstone = 1
Inv+ Магічний камінь
btn beach, Повернутися на пляж
end

:40
hut = 1
pln Ти біля примітивної хатини в джунглях. З димаря іде дим.
btn hidenhut, Сховатися і спостерігати
btn knockhut, Постукати у двері
btn beach, Повернутися на пляж
end

:hidenhut
pln Ти ховаєшся в кущах і через деякий час бачиш, як з хатини виходить високий чоловік у дивному одязі.
pln Він щось бурмоче собі під ніс і малює на землі дивні символи.
pln Судячи з усього, він чаклун або шаман.
btn jungle, Повернутися в джунглі
btn approachhut, Вийти з укриття і заговорити з ним
end

:knockhut
pln Ти стукаєш у двері хатини. Через кілька секунд їх відчиняє високий чоловік у дивному одязі.
pln "Гоблін? Тут? Цікаво..." — каже він на диво знайомою мовою.
pln "Заходь, малий. Не часто на цьому острові зустрінеш розумних істот."
btn enterhut, Увійти в хатину
btn runhut, Втекти
end

:enterhut
pln Ти входиш в хатину. Всередині повно дивних предметів, трав, сушених тварин.
pln "Мене звати Дорвуд," — каже чоловік. "Я вивчаю цей острів вже багато років. Корабель некромантів, кажеш? Цікаво..."
pln Він пропонує тобі угоду: якщо ти принесеш йому певні предмети, він допоможе тобі покинути острів.
if magicstone = 1 then btn showstone, Показати магічний камінь
if shell = 1 then btn giveshell, Віддати красиву мушлю
btn leavehut, Залишити хатину
end

:showstone
pln "Де ти це знайшов?!" — вигукує Дорвуд, побачивши камінь. "Це один з ключів до храму на горі! З цією річчю я нарешті зможу відкрити активувати портал!"
pln Він пропонує обміняти камінь на човен, щоб ти міг покинути острів.
btn givestoneboat, Віддати камінь в обмін на човен
btn keepstone, Відмовитися і залишити хатину
end

:givestoneboat
pln Ти віддаєш камінь Дорвуду. Він виглядає схвильованим і відразу починає збиратися в дорогу до гори.
pln "Човен за будинком," — каже він. "Бери його і пливи на схід. Якщо повезе за три дні будеш на Острові Черепа."
magicstone = 0
Inv- Магічний камінь
goto victory1
end

:giveshell
pln "Гарна мушля," — каже Дорвуд, розглядаючи її. "Але не думаю, що вона мені потрібна."
pln Він повертає її тобі. "Принеси щось цінніше."
btn leavehut, Піти
end

:runhut
pln Ти раптово розвертаєшся і тікаєш. Чоловік не переслідує тебе, лише здивовано дивиться вслід.
btn jungle, До джунглів
end

:keepstone
pln "Ну як знаєш," — знизує плечима Дорвуд. "Тоді мені нічим тобі допомогти."
btn leavehut, Піти
end

:leavehut
pln Ти виходиш з хатини і повертаєшся на галявину.
goto 40
end

:makeraft
pln За допомогою мотузки, бамбукових жердин і своїх спритних рук ти будуєш досить надійний маленький пліт.
pln "Тепер я можу покинути цей клятий острів," — думаєш ти.
raft = 1
Inv- Бамбукові жердини
Inv- Мотузка
Inv+ Пліт
btn launchraft, Спустити пліт на воду і відпливти
btn beach, Ще почекати
end

:launchraft
pln Ти спускаєш свій пліт на воду і відштовхуєшся від берега.
pln Пліт тримається на воді дуже добре. Ти використовуєш палицю як весло і поступово віддаляєшся від острова.
goto victory2
end

:victory1
pln **ПЕРЕМОГА!**
pln Ти знайшов човен і успішно втік з острова!
pln Ґрібзз знову на волі, готовий до нових пригод!
pln
pln Вітаємо з проходженням квесту!
btn 0, На початок
end

:victory2
pln **ПЕРЕМОГА!**
pln Власноруч збудувавши пліт, ти успішно втік з острова!
pln Ґрібзз знову на волі, готовий до нових пригод!
pln
pln Вітаємо з проходженням квесту!
btn 0, На початок
end